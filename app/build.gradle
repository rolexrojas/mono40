apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

static def createVersionCode(data) {
  def code = (data.major * 10000000) + data.build
  if (data.extra == null) {
    code += 1000000
  } else {
    code += (data.extra.major * 10000)
  }
  return code
}

static def createVersionName(data) {
  def name = "${data.major}.${data.minor}.${data.patch}"
  if (data.extra != null) {
    name = "${name}-${data.extra.name}.${data.extra.major}"
  }
  return name
}

android {
  packagingOptions {
    exclude 'META-INF/rxjava.properties'
  }

  signingConfigs {
    debug {
      storeFile rootProject.file('keystores/debug.jks')
    }
  }

  compileSdkVersion 25
  buildToolsVersion '25.0.2'

  defaultConfig {
    vectorDrawables.useSupportLibrary = true

    applicationId 'com.tpago.movil'

    def versionData = [
      major: 2,
      minor: 0,
      patch: 0,
      extra: [name: 'beta', major: 0],
      build: 30
    ]
    versionCode createVersionCode(versionData)
    versionName createVersionName(versionData)

    minSdkVersion 19
    targetSdkVersion 24

    resValue 'string', 'app_name', 'tPago'
    buildConfigField 'boolean', 'CRASHLYTICS', 'false'
  }

  buildTypes {
    debug {
      signingConfig signingConfigs.debug
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  productFlavors {
    mock {
      applicationIdSuffix '.mock'
      resValue 'string', 'app_name', 'tPago-mock'
    }
    development {
      applicationIdSuffix '.dev'
      resValue 'string', 'app_name', 'tPago-dev'
    }
    staging {
      applicationIdSuffix '.stag'
      resValue 'string', 'app_name', 'tPago-stag'
      buildConfigField 'boolean', 'CRASHLYTICS', 'true'
    }
    production {
      buildConfigField 'boolean', 'CRASHLYTICS', 'true'
    }
  }

  sourceSets {
    development.java.srcDir 'src/common/java'
    staging.java.srcDir 'src/common/java'
    production.java.srcDir 'src/common/java'
  }
}

repositories {
  jcenter()
  maven {
    url 'https://maven.fabric.io/public'
  }
  flatDir {
    dirs 'libs'
  }
  maven { url 'https://jitpack.io' }
}

dependencies {
  def autoValueVersion = '1.3'
  provided "com.jakewharton.auto.value:auto-value-annotations:${autoValueVersion}"
  annotationProcessor "com.google.auto.value:auto-value:${autoValueVersion}"

  annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'

  compile 'io.reactivex.rxjava2:rxjava:2.0.5'

  compile 'com.jakewharton.timber:timber:4.3.1'

  def okHttpVersion = '3.6.0'
  compile "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"
  compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"

  compile 'com.google.code.gson:gson:2.8.0'

  def autoValueGsonVersion = '0.4.6'
  provided "com.ryanharter.auto.value:auto-value-gson:${autoValueGsonVersion}"
  annotationProcessor "com.ryanharter.auto.value:auto-value-gson:${autoValueGsonVersion}"

  def retrofitVersion = '2.1.0'
  compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
  compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
//  compile "com.squareup.retrofit2:adapter-rxjava2:${retrofitVersion}"
  compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

  testCompile 'junit:junit:4.12'

  def supportLibraryVersion = '25.1.1'
  compile "com.android.support:appcompat-v7:${supportLibraryVersion}"
  compile "com.android.support:design:${supportLibraryVersion}"
  compile "com.android.support:percent:${supportLibraryVersion}"
  compile "com.android.support:recyclerview-v7:${supportLibraryVersion}"

  compile('com.crashlytics.sdk.android:crashlytics:2.6.6@aar') {
    transitive = true
  }

  def daggerVersion = '2.9'
  compile "com.google.dagger:dagger:${daggerVersion}"
  annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

  compile 'uk.co.chrisjenx:calligraphy:2.2.0'

  def butterKnifeVersion = '8.5.1'
  compile "com.jakewharton:butterknife:${butterKnifeVersion}"
  annotationProcessor "com.jakewharton:butterknife-compiler:${butterKnifeVersion}"

  compile 'com.theartofdev.edmodo:android-image-cropper:2.3.1'

  compile 'com.squareup.picasso:picasso:2.5.2'
  compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'
  compile 'jp.wasabeef:picasso-transformations:2.1.0'

  compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

  compile fileTree(include: ['*.jar'], dir: 'libs')
  compile(name: 'CubeSdk', ext: 'aar')

  // Deprecated dependencies
  compile "com.github.ozodrukh:CircularReveal:2.0.1"
  compile "com.googlecode.libphonenumber:libphonenumber:8.0.1"
  compile "com.jakewharton.rxbinding:rxbinding:0.4.0"
  compile "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}"
  compile "com.tbruyelle.rxpermissions:rxpermissions:0.9.0"
  compile "com.yqritc:recyclerview-flexibledivider:1.4.0"
  compile "de.hdodenhof:circleimageview:2.1.0"
  compile "io.reactivex:rxandroid:1.2.1"
  compile "io.reactivex:rxjava:1.2.2"
}

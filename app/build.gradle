apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'

def appName = 'tPago'
def version = [
  name: { major, minor, patch, build, suffix = null ->
    def name = "$major.$minor.$patch"
    if (suffix) {
      name <<= "-$suffix"
    }
    return name.toString()
  },
  code: { major, minor, patch, build ->
    return (major * 10_000_000) + (minor * 100_000) + (patch * 1_000) + build
  }
]

android {
  packagingOptions {
    exclude 'META-INF/rxjava.properties'
  }

  compileSdkVersion 25
  buildToolsVersion '25.0.3'

  defaultConfig {
    applicationId 'com.tpago.movil'

    def (major, minor, patch, build) = [2, 2, 0, 1]
    versionName version.name(major, minor, patch, build, 'beta')
    versionCode version.code(major, minor, patch, build)

    versionName '2.2.0-beta1'
    versionCode 20_200_001

    minSdkVersion 19
    targetSdkVersion 25

    vectorDrawables.useSupportLibrary = true

    javaCompileOptions {
      annotationProcessorOptions {
        arguments = [
          'eventBusIndex': 'com.tpago.movil.bus.BusIndex',
          'autovaluegson.defaultCollectionsToEmpty': 'true',
          'autovaluegson.mutableAdaptersWithDefaultSetters': 'true'
        ]
      }
    }
  }

  productFlavors {
    development {
      def flavorName = 'dev'

      applicationIdSuffix ".$flavorName"

      resValue 'string', 'app_name', "$appName-$flavorName"

      buildConfigField 'boolean', 'MODE_CRASH_REPORTING', 'false'
    }

    staging {
      def flavorName = 'stag'

      applicationIdSuffix ".$flavorName"

      resValue 'string', 'app_name', "$appName-$flavorName"

      buildConfigField 'boolean', 'MODE_CRASH_REPORTING', 'true'
    }

    production {
      resValue 'string', 'app_name', appName

      buildConfigField 'boolean', 'MODE_CRASH_REPORTING', 'true'
    }
  }

  signingConfigs {
    debug {
      storeFile rootProject.file('keystores/debug.jks')
    }
  }

  buildTypes {
    debug {
      signingConfig signingConfigs.debug
    }

    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  applicationVariants.all { variant ->
    variant.outputs.each { output ->
      output.outputFile = new File(
        output.outputFile.parent,
        "$appName-v${variant.versionName}-${variant.name}.apk"
      )
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

retrolambda {
  javaVersion JavaVersion.VERSION_1_7
}

repositories {
  // Local repositories
  flatDir {
    dirs 'libs'
  }

  // Remote repositories
  jcenter()

  maven {
    url 'https://maven.fabric.io/public'
  }

  maven {
    url 'https://jitpack.io'
  }
}

dependencies {
  // Local dependencies
  compile fileTree(include: ['*.jar'], dir: 'libs')

  def cubeSdkName = { env -> "Cub3AltPanSDK-v1.4-$env" }
  developmentCompile(name: cubeSdkName('dev'), ext: 'aar')
  stagingCompile(name: cubeSdkName('stag'), ext: 'aar')
  productionCompile(name: cubeSdkName('prod'), ext: 'aar')

  // Remove dependencies
  compile 'com.google.guava:guava:23.0-android'

  def autoValueVersion = '1.4'
  provided "com.jakewharton.auto.value:auto-value-annotations:$autoValueVersion"
  annotationProcessor "com.google.auto.value:auto-value:$autoValueVersion"

  annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'

  compile 'com.jakewharton.timber:timber:4.5.1'

  def okHttpVersion = '3.8.1'
  compile "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"
  compile "com.squareup.okhttp3:okhttp:$okHttpVersion"

  compile 'com.google.code.gson:gson:2.8.1'

  def autoValueGsonVersion = '0.5.0'
  provided "com.ryanharter.auto.value:auto-value-gson:$autoValueGsonVersion"
  annotationProcessor "com.ryanharter.auto.value:auto-value-gson:$autoValueGsonVersion"

  def retrofitVersion = '2.3.0'
  compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
  compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"

  compile 'com.birbit:android-priority-jobqueue:2.0.1'

  compile 'org.greenrobot:eventbus:3.0.0'
  annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.0.1'

  def supportLibraryVersion = '25.3.1'
  compile "com.android.support:appcompat-v7:$supportLibraryVersion"
  compile "com.android.support:design:$supportLibraryVersion"
  compile "com.android.support:percent:$supportLibraryVersion"
  compile "com.android.support:recyclerview-v7:$supportLibraryVersion"

  compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
    transitive = true
  }

  def daggerVersion = '2.11'
  compile "com.google.dagger:dagger:$daggerVersion"
  annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

  compile 'uk.co.chrisjenx:calligraphy:2.3.0'

  def butterKnifeVersion = '8.7.0'
  compile "com.jakewharton:butterknife:$butterKnifeVersion"
  annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnifeVersion"

  compile 'com.theartofdev.edmodo:android-image-cropper:2.4.3'

  compile 'com.squareup.picasso:picasso:2.5.2'
  compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

  // Deprecated dependencies
  compile "com.github.ozodrukh:CircularReveal:2.0.1"
  compile "com.googlecode.libphonenumber:libphonenumber:8.0.1"
  compile "com.jakewharton.rxbinding:rxbinding:0.4.0"
  compile "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
  compile "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"
  compile "com.tbruyelle.rxpermissions:rxpermissions:0.9.0"
  compile "com.yqritc:recyclerview-flexibledivider:1.4.0"
  compile "de.hdodenhof:circleimageview:2.1.0"
  compile "io.reactivex:rxandroid:1.2.1"
  compile "io.reactivex:rxjava:1.2.2"
  compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
  compile 'io.reactivex.rxjava2:rxjava:2.0.5'
  compile 'javax.annotation:jsr250-api:1.0'
  compile 'jp.wasabeef:picasso-transformations:2.1.0'
}
